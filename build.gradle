plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

defaultTasks 'clean', 'shadowJar', 'build'

allprojects {
    group = 'me.xneox'
    version = '6.0.0'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    // Fix encoding on windows...
    compileJava {
        options.encoding = "UTF-8"
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.github.johnrengelman.shadow"

    processResources {
        expand projectVersion: project.version
    }

    repositories {
        mavenCentral()
        maven { url = 'https://repo.maven.apache.org/maven2/' }
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://repo.panda-lang.org/' }
    }

    dependencies {
        // Shaded dependencies.
        implementation 'com.maxmind.geoip2:geoip2:2.15.0'
        implementation 'org.apache.commons:commons-compress:1.20'
        implementation 'org.apache.commons:commons-text:1.9'
        implementation 'com.google.guava:guava:30.1.1-jre'
        implementation 'com.github.simplix-softworks:SimplixStorage:3.2.3'
        implementation 'org.spongepowered:configurate-hocon:4.1.1'

        // Provided dependencies.
        compileOnly 'org.apache.logging.log4j:log4j-core:2.14.1'
        compileOnly 'org.slf4j:slf4j-api:1.7.32'
    }

    shadowJar {
        minimize()

        relocate 'org.bstats', 'me.xneox.epicguard.libs.bstats'
        relocate 'com.fasterxml', 'me.xneox.epicguard.libs.fasterxml'
        relocate 'com.maxmind', 'me.xneox.epicguard.libs.maxmind'
        relocate 'de.leonhard.storage', 'me.xneox.epicguard.libs.storage'
        relocate 'com.google.common', 'me.xneox.epicguard.libs.google.common'
        relocate 'org.spongepowered.configurate', 'me.xneox.epicguard.libs.configurate'
        relocate 'org.yaml.snakeyaml', 'me.xneox.epicguard.libs.snakeyaml'
        relocate 'io.leangen.geantyref', 'me.xneox.epicguard.libs.geantyref'
        relocate 'com.typesafe.config', 'me.xneox.epicguard.libs.config'

        // Exclude useless text files
        exclude 'mozilla/'
        exclude 'org/apache/commons/codec/'
    }
}

// Include all platforms in one jar.
dependencies {
    implementation project(path: ':core', configuration: 'shadow')
    implementation project(path: ':bukkit', configuration: 'shadow')
    implementation project(path: ':bungee', configuration: 'shadow')
    implementation project(path: ':velocity', configuration: 'shadow')
}